plugins {
	id 'fabric-loom'
	id 'maven-publish'
}

version = System.getProperty("loom.test.version")
group = "com.example"

dependencies {
	minecraft "com.mojang:minecraft:1.16.5"
	mappings "net.fabricmc:yarn:1.16.5+build.5:v2"
	modImplementation "net.fabricmc:fabric-loader:0.11.2"
	modCompileOnlyApi fabricApi.module('fabric-biome-api-v1', '0.42.0+1.16')
	modRuntimeOnly fabricApi.module('fabric-command-api-v1', '0.42.0+1.16')
}

base {
	archivesName = "fabric-example-lib"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	withSourcesJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact(remapJar) {
				classifier "classifier"
			}
		}
	}

	repositories {
		maven {
			url "http://localhost:${System.getProperty("loom.test.mavenPort")}/"
			allowInsecureProtocol = true
		}
	}
}
