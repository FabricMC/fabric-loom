plugins {
    id 'java'
    id 'groovy'
    id 'maven'

    id 'idea'
    id 'eclipse'

    id 'net.minecrell.licenser' version '0.2.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'net.fabricmc'
archivesBaseName = project.name.toLowerCase()
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'fabric'
        url = 'http://maven.fabricmc.net/'
    }
	maven {
        name = 'cuchaz'
        url = 'http://maven.cuchazinteractive.com/'
    }
}

configurations {
    deployerJars
    shade
    compile.extendsFrom shade
}

dependencies {
    compile gradleApi()
    shade 'org.zeroturnaround:zt-zip:1.9'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.slf4j:slf4j-simple:1.7.21'
    shade 'com.google.code.gson:gson:2.6.2'
    shade 'commons-io:commons-io:1.4'
    shade 'com.google.guava:guava:19.0'
    shade 'net.fabricmc:blending-jar:0.1.0'

	shade 'net.fabricmc:enigma:0.11.+'
	shade 'org.javassist:javassist:3.+'
	shade 'org.bitbucket.mstrobel:procyon-decompiler:0.5.28-enigma'
	shade 'org.javassist:javassist:3.20.0-GA'
	shade 'de.sciss:syntaxpane:1.1.+'
}

license {
    header file('HEADER')
    include '**/*.java'
}

sourceSets {
    main.compileClasspath += configurations.shade;
    main.runtimeClasspath += configurations.shade;
    test.compileClasspath += configurations.shade;
    test.runtimeClasspath += configurations.shade;
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty('mavenPass')) {
                repository(url: 'http://mavenupload.modmuss50.me/') {
                    authentication(userName: 'buildslave', password: project.getProperty('mavenPass'))
                }
            } else {
                repository(url: 'file:///var/www/maven/')
            }

            pom {
                artifactId = project.archivesBaseName

                project {
                    name = project.name
                    description = project.description
                    packaging = 'jar'
                    url = project.url

                    scm {
                        url = 'https://github.com/FabricMC/fabric-loom'
                        connection = 'scm:git:git@github.com:FabricMC/fabric-loom.git'
                        developerConnection = 'scm:git:git@github.com:FabricMC/fabric-loom.git'
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/FabricMC/fabric-loom/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/FabricMC/fabric-loom/blob/master/LICENSE'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'modmuss50'
                            name = 'modmuss50'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}
